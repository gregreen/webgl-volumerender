<!doctype html>
<html>
<head>
  <title>WebGL Volume Renderer</title>
  <meta charset="utf-8"/>
  <meta name="viewport" content="width=device-width, initial-scale=1"/>
  <link href="volumerender.css" rel="stylesheet" type="text/css"/>
  <script src="https://ajax.googleapis.com/ajax/libs/jquery/2.1.3/jquery.min.js"></script>
  <script src="volumerender.js" type="text/javascript"> </script>
</head>
<body>
  <h4>WebGL Volume Renderer</h4>
  <canvas id="gl-canvas" width="480" height="240">
    Your browser does not seem to support the HTML5 <code>&lt;canvas&gt;</code> element.
  </canvas>
  <p><span id="fps"></span><span> frames / second</span></p>
  <p>To control the camera, press: &#8592; &#8593; &#8594; &#8595; f d</p>
  <script type="x-shader/x-vertex" id="vertex-shader">
    //#version 100
    attribute vec2 position;

    void main() {
      gl_Position = vec4(position, 0.0, 1.0);
    }
  </script>
  <script type="x-shader/x-fragment" id="fragment-shader">
    //#version 100
    precision mediump float;
    uniform vec2 canvasSize;
    uniform sampler2D uSampler[8];
    uniform float time;
    uniform vec3 cameraOrigin;
    uniform mat4 cameraRot;

    #define pi 3.14159
    #define fov 0.50*pi         // Field-of-view of camera
    #define n_steps_max 150     // # of steps to take for each ray
    #define ray_max_dist 10.0   // Maximum distance for ray
    #define tau_max 3.0         // Truncate ray after reaching this optical depth
    #define xi 15.0             // Overall density scaling
    #define r_max2 (15.*15.)    // Maximum radius (in z=0 plane) to render
    #define z_max 1.0           // Maximum z to render
    #define eps 0.0001          // A small number

    float step_size = (ray_max_dist / float(n_steps_max));
    float s_max = tau_max / step_size;
    float screen_dist = 1. / sin(0.5*fov);

    // Calculate distance, s, at which the ray
    //   $\vec{x}_0 + s \cdot \vec{\delta x}$
    // intersects the plane
    //   $\vec{a} \cdot \vec{r} = c$.
    float plane_intersection(vec3 a, float c, vec3 x0, vec3 dx) {
      float denom = dot(a, dx);
      if(abs(denom) < eps) {
        return -1.;
      }
      return (c - dot(a, x0)) / denom;
    }

    // Calculate distance, s, at which the ray
    //   $\vec{x}_0 + s \cdot \vec{\delta x}$
    // intersects the cylinder (w/o caps)
    //   $x^2 + y^2 = r^2$.
    vec2 cylinder_intersections(float r2, vec3 x0, vec3 dx) {
      float a = dx.x*dx.x + dx.y*dx.y;
      float b = 2. * (x0.x*dx.x + x0.y*dx.y);
      float c = x0.x*x0.x + x0.y*x0.y - r2;

      float discriminant = b*b - 4.*a*c;
      if(discriminant <= 0.) {
        return vec2(-1., -1.);
      }

      float k = 1. / (2. * a);
      float disc_term = k * sqrt(discriminant);
      float b_term = -k * b;

      return vec2(b_term + disc_term, b_term - disc_term);
    }

    // Calculate the intersections of a ray with a cylinder defined
    // by a radius r and height h (extending to both z = +-h).
    vec4 ray_endpoints(float r2, float h, vec3 x0, vec3 dx) {
      // Get all the intersection distances
      vec4 s = vec4(
        plane_intersection(vec3(0., 0., 1.), h, x0, dx),
        plane_intersection(vec3(0., 0., -1.), h, x0, dx),
        cylinder_intersections(r2, x0, dx)
      );

      // Filter out intersections that are out of bounds
      vec3 x = x0 + s[0] * dx;
      if(x.x*x.x + x.y*x.y > r2) {
        s[0] = -1.;
      }

      x = x0 + s[1] * dx;
      if(x.x*x.x + x.y*x.y > r2) {
        s[1] = -1.;
      }

      x = x0 + s[2] * dx;
      if((x.z < -h) || (x.z > h)) {
        s[2] = -1.;
      }

      x = x0 + s[3] * dx;
      if((x.z < -h) || (x.z > h)) {
        s[3] = -1.;
      }

      return s;
    }

    vec2 select_endpoints(vec4 s) {
      // Find the maximum ray distance
      float s_end = max(max(s[0], s[1]), max(s[2], s[3]));

      if(s_end <= 0.) {
        return vec2(0., 0.);
      }

      // Remove the maximum value
      if(s[0] >= s_end - eps) {
        s[0] = -1.;
      } else if(s[1] >= s_end - eps) {
        s[1] = -1.;
      } else if(s[2] >= s_end - eps) {
        s[2] = -1.;
      } else {
        s[3] = -1.;
      }

      // Find the second largest ray distance
      float s_start = max(max(s[0], s[1]), max(s[2], s[3]));

      if(s_start <= 0.) {
        return vec2(0., s_end);
      }

      return vec2(s_start, s_end);
    }

    // Orthographic projection
    vec3 unit_ray(vec2 uv) {
    	return normalize(vec3(uv, screen_dist));
    }

    // Random number generator
    float rand(vec2 co){
      return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
    }

    // Convert Cartesian to Spherical (physics convention)
    vec3 xyz2tpr(vec3 xyz) {
      float r = length(xyz);
      return vec3(acos(xyz[2]/r), atan(xyz[1], xyz[0]), r);
    }

    // Map texture values to density (textures don't store density linearly,
    // because of limited, 8-bit precision)
    float map_density_val(float v) {
      return v*v*v*v;
    }

    // Look up density at position (x,y,z) in space, by querying appropriate
    // texture.
    float density(vec3 xyz) {
      vec3 tpr = xyz2tpr(xyz);  // Convert to spherical coords

      if(tpr[2] > 63.096) {
        return 0.;
      }
      // Convert to texture (u,v) coords
      vec2 uv = vec2(tpr[1]/(2.*pi), tpr[0]/pi);

      // Determine which texture/channel to load
      // based on distance from origin.
      // Go from outside to inside, b/c largest
      // radii cover most volume.
      // Written as a binary search tree to minimize
      // number of comparisons.
      if(tpr[2] < 2.00) {
        if(tpr[2] < 0.316) {
          if(tpr[2] < 0.126) {
            if(tpr[2] < 0.0794) {
              if(tpr[2] < 0.0631) {
                // < 0.0631
                return texture2D(uSampler[0], uv)[0];
              } else {
                // < 0.0794
                return texture2D(uSampler[0], uv)[1];
              }
            } else {
              if(tpr[2] < 0.100) {
                // < 0.100
                return texture2D(uSampler[0], uv)[2];
              } else {
                // < 0.126
                return texture2D(uSampler[0], uv)[3];
              }
            }
          } else {
            if(tpr[2] < 0.200) {
              if(tpr[2] < 0.158) {
                // < 0.158
                return texture2D(uSampler[1], uv)[0];
              } else {
                // < 0.200
                return texture2D(uSampler[1], uv)[1];
              }
            } else {
              if(tpr[2] < 0.251) {
                // < 0.251
                return texture2D(uSampler[1], uv)[2];
              } else {
                // < 0.316
                return texture2D(uSampler[1], uv)[3];
              }
            }
          }
        } else { // 0.316 < d < 2.00
          if(tpr[2] < 0.794) {
            if(tpr[2] < 0.501) {
              if(tpr[2] < 0.398) {
                // < 0.398
                return texture2D(uSampler[2], uv)[0];
              } else {
                // < 0.501
                return texture2D(uSampler[2], uv)[1];
              }
            } else {
              if(tpr[2] < 0.631) {
                // < 0.631
                return texture2D(uSampler[2], uv)[2];
              } else {
                // < 0.794
                return texture2D(uSampler[2], uv)[3];
              }
            }
          } else {
            if(tpr[2] < 1.26) {
              if(tpr[2] < 1.00) {
                // < 1.00
                return texture2D(uSampler[3], uv)[0];
              } else {
                // < 1.26
                return texture2D(uSampler[3], uv)[1];
              }
            } else {
              if(tpr[2] < 1.58) {
                // < 1.58
                return texture2D(uSampler[3], uv)[2];
              } else {
                // < 2.00
                return texture2D(uSampler[3], uv)[3];
              }
            }
          }
        }
      } else { // d > 2.00
        if(tpr[2] < 12.59) {
          if(tpr[2] < 5.01) {
            if(tpr[2] < 3.16) {
              if(tpr[2] < 2.51) {
                // < 2.51
                return texture2D(uSampler[4], uv)[0];
              } else {
                // < 3.16
                return texture2D(uSampler[4], uv)[1];
              }
            } else {
              if(tpr[2] < 3.98) {
                // < 3.98
                return texture2D(uSampler[4], uv)[2];
              } else {
                // < 5.01
                return texture2D(uSampler[4], uv)[3];
              }
            }
          } else {
            if(tpr[2] < 7.94) {
              if(tpr[2] < 6.31) {
                // < 6.31
                return texture2D(uSampler[5], uv)[0];
              } else {
                // < 7.94
                return texture2D(uSampler[5], uv)[1];
              }
            } else {
              if(tpr[2] < 10.0) {
                // < 10.00
                return texture2D(uSampler[5], uv)[2];
              } else {
                // < 12.59
                return texture2D(uSampler[5], uv)[3];
              }
            }
          }
        } else {
          if(tpr[2] < 31.62) {
            if(tpr[2] < 19.95) {
              if(tpr[2] < 15.85) {
                // < 15.85
                return texture2D(uSampler[6], uv)[0];
              } else {
                // < 19.95
                return texture2D(uSampler[6], uv)[1];
              }
            } else {
              if(tpr[2] < 25.12) {
                // < 25.12
                return texture2D(uSampler[6], uv)[2];
              } else {
                // < 31.62
                return texture2D(uSampler[6], uv)[3];
              }
            }
          } else {
            if(tpr[2] < 50.12) {
              if(tpr[2] < 39.81) {
                // < 39.81
                return texture2D(uSampler[7], uv)[0];
              } else {
                // < 50.12
                return texture2D(uSampler[7], uv)[1];
              }
            } else {
              // < 63.10
              return texture2D(uSampler[7], uv)[2];
            }
          }
        }
      }
    }

    // Integrate a ray (through the density field) along x0 + s*dx
    float integrate_ray(vec3 x0, vec3 dx, int n_steps) {
      float s = 0.;
      vec2 dxy = vec2(dx.x, dx.y);
      vec3 x = x0 + dx * rand(dxy);

      for(int k=0; k<n_steps_max; k++) {
        x += dx;
        s += map_density_val(density(x));

        // Truncate ray after reaching some large optical depth
        if((k > n_steps) || (xi*s > s_max)) { break; }
      }

      return xi*s;
    }

    void main() {
      vec2 xy_raw = vec2(gl_FragCoord.x, gl_FragCoord.y);

      // Add a sub-pixel shift to the screen pixel location (to avoid aliasing)
      vec2 offset = vec2(rand(xy_raw), rand(xy_raw));
      vec2 screen_xy = xy_raw + offset;

      // Map screen (x,y) to standard rectangle:
      //   * Long axis goes from -1 to 1
      //   * Short axis has correct aspect ratio
      float mx = max(canvasSize[0], canvasSize[1]);
      vec2 uv = 2. * vec2(screen_xy.x-0.5*canvasSize[0], screen_xy.y-0.5*canvasSize[1]) / mx;

      // Camera ray direction (i.e., projection)
      vec4 dx4 = vec4(unit_ray(uv), 1.);
      dx4 = cameraRot * dx4;
      vec3 dx = step_size * vec3(dx4[0], dx4[1], dx4[2]);

      // Determine where to begin and end ray
      vec4 s_intersections = ray_endpoints(r_max2, z_max, cameraOrigin, dx);
      vec2 s_endpoints = select_endpoints(s_intersections); // start/end distances (in units of step_size)

      vec3 x0 = cameraOrigin + s_endpoints[0] * dx;
      int n_steps = int(s_endpoints[1] - s_endpoints[0]);
      if(n_steps > n_steps_max) {
        n_steps = n_steps_max;
      }

      // Integrate to get optical depth
      float tau = step_size * integrate_ray(x0, dx, n_steps);

      // Convert optical depth to opacity
  	  float opacity = 1. - exp(-tau);

      gl_FragColor = vec4(opacity, opacity, opacity, 1.0);
      //gl_FragColor = vec4(0.0, 1.0, 0.0, 1.0);
      //gl_FragColor = texture2D(uSampler[0], uv);
      // gl_FragColor = vec4(
      //   step_size * max(s_intersections[2], 0.) + max(s_intersections[3], 0.),
      //   step_size * s_intersections[0],
      //   step_size * s_intersections[1],
      //   1.
      // );
      // gl_FragColor = vec4(
      //   0.,
      //   step_size * s_endpoints[1],
      //   step_size * s_endpoints[0],
      //   1.
      // );
      //gl_FragColor = vec4(s_endpoints[0]/step_size*dx, 1.);
      //gl_FragColor = vec4(0., float(n_steps)/100., 0., 1.);
    }
  </script>
</body>
</html>
